plugins {
    id "com.palantir.jacoco-full-report" version "0.4.0"
    id "com.avast.gradle.docker-compose" version "0.8.8"
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }
}

subprojects {
    if (project.name == 'tracker' || project.name == 'reporter') {
        apply plugin: 'java'
        apply plugin: 'jacoco'

        tasks.withType(Test) {
            useTestNG() {
                useDefaultListeners = true
            }

            testLogging {
                showStandardStreams = true
                events 'started', 'passed', 'skipped', 'failed'
            }

            outputs.upToDateWhen { false }
        }

        test.dependsOn ':composeUp'

        jacocoTestReport {
            dependsOn tasks.withType(Test)
            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }
}

configurations { codacy }

dependencies {
    codacy 'com.codacy:codacy-coverage-reporter:+'
}

jacocoFullReport.dependsOn ':tracker:jacocoTestReport', ':reporter:jacocoTestReport'

task sendCoverageToCodacy(type: JavaExec) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = ["report", "-l", "Java", "-r", jacocoFullReport.reports.xml.destination.toString()]
}

dockerCompose {
    startedServices = ['db', 'mq']
    projectName = null
}